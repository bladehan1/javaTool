plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.2'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
def protobufVersion = "3.25.5"
def grpcVersion = "1.60.0"

dependencies {
    // protobuf - 更新到与插件兼容的版本
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion
   //grpc
    implementation group: 'io.grpc', name: 'grpc-netty', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    //appache httpclient
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'com.google.guava:guava:30.1.1-jre'
    // elastic
    implementation 'org.elasticsearch:elasticsearch:7.10.0'
    implementation 'org.elasticsearch.client:transport:7.10.0'
    implementation 'org.elasticsearch.client:elasticsearch-rest-client:7.10.0'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.10.0'
    // Alibaba json
    implementation 'com.alibaba:fastjson:1.2.70'
    // Annotation 依赖
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    // 日志
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
}

task runGrpcServer(type: JavaExec) {
    group = 'application'
    description = '运行服务器'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'grpc.tron.server.AdvancedTronServer'
    standardInput = System.in
}
task runGrpcClient(type: JavaExec) {
    group = 'application'
    description = '运行客户端'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'grpc.tron.client.TronClientTest'
    standardInput = System.in
}



sourceSets {
    main {
        proto {
            srcDir 'src/main/protos'
        }
        java {
            srcDir 'src/main/gen'
            srcDir 'src/main/java'
        }
    }
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = "gen"
                }
            }
        }
        all()*.plugins {
            grpc {
                outputSubDir = "gen"
            }
        }
    }
}

clean.doFirst {
    delete "src/main/gen"
}

//// 确保在编译前生成代码
//compileJava.dependsOn generateProto
