buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
        }
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
        classpath "com.github.node-gradle:gradle-node-plugin:7.1.0"
    }
}



apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
}

def protobufVersion = '3.25.8'
def grpcVersion = '1.75.0'
def protocGenVersion = '1.60.0' // https://github.com/grpc/grpc-java/pull/11371 , 1.64.x is not supported CentOS 7.


sourceSets {
    main {
        proto {
            srcDir 'src/main/protos'
        }
        java {
            srcDir 'src/main/gen'
            srcDir 'src/main/java'
        }
    }
}

dependencies {
    // grpc
    implementation group: 'io.grpc', name: 'grpc-netty', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
    implementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion

    // google protobuf
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    //appache httpclient
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'com.google.guava:guava:30.1.1-jre'
    // elastic
    implementation 'org.elasticsearch:elasticsearch:7.10.0'
    implementation 'org.elasticsearch.client:transport:7.10.0'
    implementation 'org.elasticsearch.client:elasticsearch-rest-client:7.10.0'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.10.0'
    // Alibaba json
    implementation 'com.alibaba:fastjson:1.2.70'
    // Annotation 依赖
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    // 日志 - 统一使用 SLF4J + logback
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'ch.qos.logback:logback-classic:1.2.13'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = "gen"
                }
            }
        }
        all()*.plugins {
            grpc {
                outputSubDir = "gen"
            }
        }
    }
}

clean.doFirst {
    delete "src/main/gen"
}

// 修复任务依赖关系
tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    // 明确指定资源目录，避免隐式依赖
    from('src/main/resources') {
        exclude '**/*.proto'  // 排除 proto 文件
        exclude '**/protos/**' // 排除 protos 目录
    }
}

// 确保编译前生成 proto 代码
tasks.named('compileJava') {
    dependsOn 'generateProto'
}
processResources {
    dependsOn 'generateProto'

}



task runGrpcServer(type: JavaExec) {
    group = 'application'
    description = '运行服务器'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'grpc.tron.server.AdvancedTronServer'
    standardInput = System.in
}
task runGrpcClient(type: JavaExec) {
    group = 'application'
    description = '运行客户端'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'grpc.tron.client.TronClientTest'
    standardInput = System.in
}


